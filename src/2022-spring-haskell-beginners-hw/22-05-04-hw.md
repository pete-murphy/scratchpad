*_Review part one: magic syntax_*

So far we've been working with a custom `List`
```
data List a = Nil | Cons a (List a)
```
but Haskell has special list syntax, so the built-in data definition might look like this (if we could define it in userland)
```
-- (Pseudo Haskell, not actually valid syntax)
data [a] = [] | a : [a]
```
so all of the following are actually equivalent (and these are all valid Haskell)
```
>>> (:) 1 ((:) 2 ((:) 3 []))
>>> 1 : 2 : 3 : []
>>> [1, 2, 3]
```

Similarly, we can think of built-in tuples as having definitions like
```
data (a, b) = (a, b)
data (a, b, c) = (a, b, c)
data (a, b, c, d) = (a, b, c, d)
...
```

*_Review part two: type classes_*

This is all the code from the last session
```
data Foo = Bar | Baz deriving (Eq, Show, Ord)

class Bounded' a where
  minBound' :: a
  maxBound' :: a

instance Bounded' Foo where
  minBound' = Bar
  maxBound' = Baz

instance Bounded' Bool where
  minBound' = False
  maxBound' = True

instance (Bounded' a, Bounded' b) => Bounded' (a, b) where
  minBound' = (minBound', minBound')
  maxBound' = (maxBound', maxBound')
```
and yet from no more than that the compiler was able to do lots—potentially infinite amounts—of code generation for us!
```
>>> minBound' :: (Foo, Bool)
(Bar,False)
>>> maxBound' :: ((Foo, Foo), (Foo, (Bool, Bool)))
((Baz,Baz),(Baz,(True,True)))
```

*_Homework part one: mechanical instances_*

Let's define a type similar to the `data Foo = Bar | Baz` we used in class, but with a type parameter on each data constructor
```
data OneOf a b = This a | That b deriving (Show)
```
We're deriving `Show` but in class we learned that the compiler can derive instances for other type classes as well. For the sake of getting practice hands-on-keyboards-typing-Haskell, let's give the compiler a break and try to write some of these instances ourselves.
```
-- | ** Exercise 1a: **
-- Can we define an `Eq` instance for `OneOf`?
-- What constraints do we need to have on `a` and/or `b`?
-- Is there more than one way of defining this instance?
-- Fill in the `(???)` if you can, remember the `case ... of ...` recipes!
instance (???) => Eq (OneOf a b) where
  (==) x y = (???)

-- | ** Exercise 1b: **
-- How about an `Ord` instance for `OneOf`?
-- Again, what constraints do we need to have? are there other implementations?
instance (???) => Ord (OneOf a b) where
  (<=) x y = (???)

-- | ** Exercise 1c: **
-- Same for `Bounded'`: assumptions, alternative implementations.
instance (???) => Bounded' (OneOf a b) where
  minBound' = (???)
  maxBound' = (???)

-- | ** Exercise 1d: **
-- Did you get this far? do you feel good about the instance you just wrote?!?
-- Without typing it in a REPL, can you determine if the following expression
-- would compile?
-- >>> minBound' :: OneOf (OneOf (Bool, Bool) Foo, Foo) (Bool, (Bool, Foo))

-- What else would we need in order to get the following expression to compile?
-- >>> minBound' :: OneOf (OneOf (Bool, Bool) Foo, Foo) (Int, (Bool, Foo))
```

*_Homework part two: more type classes_*

In the last session, we briefly talked about lattices, which could be defined as a type class.
```
class Lattice a where
  meet :: a -> a -> a
  join :: a -> a -> a
```
We can visualize what `meet` and `join` mean in the context of a partially-ordered set:
```
    A
  ╱ │ ╲
 ╱  │  ╲
B   C   D
│╲ ╱ ╲ ╱│
│ ╲   ╲ │
│╱ ╲ ╱ ╲│
E   F   G
 ╲  │  ╱
  ╲ │ ╱
    H
```
Supposing we had this as a data type
```
data OneOfEight = A | B | C | D | E | F | G | H deriving (Eq)
```
we could define a `Lattice` instance for `OneOfEight` such that `join` takes two `OneOfEight`s and returns the "least upper bound"—so `join A B == A`, `join E G == C` and `join D E == A`. Similarly, `meet` gives the "greatest lower bound"—so `meet A B == B`, `meet E G == H` and `meet D E == H`.
```
{-
Head's up: you can only have one of these instances for `Lattice (a, b)` in scope at a time since they overlap,
so you'll have to comment out whichever ones you're not working on.
-}

-- | ** Exercise 2a: **
-- Can we define a `Lattice` instance with the following "instance head"?
-- (HINT: Remember our `Bounded' (a, b)` instance)

instance (Lattice a, Lattice b) => Lattice (a, b) where
  meet (a,b) (a',b') = (???)
  join (a,b) (a',b') = (???)

-- | ** Exercise 2b: **
-- What if we changed the instance head to this, can we still implement it?
-- (HINT: If you think we can, try searching Hoogle for `Ord a => a -> a -> a`)

-- instance (Ord a, Ord b) => Lattice (a, b) where
--   meet = (???)
--   join = (???)

-- | ** Exercise 2c: **
-- What about this one?

-- instance (Bounded' a, Bounded' b) => Lattice (a, b) where
--   meet = (???)
--   join = (???)

-- | ** Exercise 2d: **
-- What about this one?

instance (???) => Lattice (OneOf a b) where
  meet = (???)
  join = (???)
```